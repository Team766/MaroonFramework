plugins {
    id 'java-library'
    id 'maven-publish'
    id 'edu.wpi.first.GradleRIO' version '2022.1.1'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

allprojects {
    group 'com.team766'
    version '1.0.0'

    repositories {
        mavenCentral()

        maven { url = uri('https://frcmaven.wpi.edu/artifactory/release/') }

        // CTRE
        maven { url = uri('https://devsite.ctr-electronics.com/maven/release/') }

        // REV
        maven { url = uri('https://maven.revrobotics.com/') }
    }
}

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)

    // CTRE is implementation because it is not a hard requirement
    implementation 'com.ctre.phoenix:api-java:5.20.2'
    implementation 'com.ctre.phoenix:wpiapi-java:5.20.2'

    // REV is implementation because it is not a hard requirement
    implementation 'com.revrobotics.frc:REVLib-java:2022.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Team766/MaroonFramework")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

test {
    useJUnitPlatform()
}

wpi.java.configureTestTasks(test)
